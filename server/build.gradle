plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'maven-publish'
	id 'jacoco'
	// run Sonarqube from gradle task
	id "org.sonarqube" version "2.8"
}

group = 'com.spring-vue-gradle'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from(components.java)
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'io.cucumber:cucumber-java:5.6.0'
	testImplementation 'io.cucumber:cucumber-junit:5.6.0'
	implementation 'org.hibernate:hibernate-search:5.11.1.Final'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.mariadb.jdbc:mariadb-java-client'
	runtimeOnly 'com.h2database:h2'
	testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.0.0.RELEASE'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	minHeapSize = "128m"
	maxHeapSize = "2048m"
	testLogging.showStandardStreams = true
}

// Task used for just running cucumber tests, and can specify tags with -Ptags="@sometags"
task cucumber() {
	dependsOn assemble, compileTestJava
	String selectedTags = findProperty("tags") ?: ""

	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
					'--plugin', 'pretty', // How to format test report, "pretty" is good for human eyes
					'--glue', 'com.springvuegradle.team6.steps', // Where to look for your tests' steps
					'src/test/resources/edmun.features/', // Where to look for your features
					'--strict', // Causes cucumber to fail if any step definitions are still undefined
					'--tags', selectedTags // Only runs certain tagged scenarios of tags arg is given
			]
		}
	}
}

// Jacoco test coverage configuration
jacoco {
	toolVersion = "0.8.2"
}
// select what reports to generate and where to put them
jacocoTestReport {
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/jacoco/test-report.xml")
		csv.enabled false
		html.destination file("${buildDir}/jacoco/html")
	}
}

// Sonarqube specific configuration
sonarqube {
	properties {
		property "sonar.projectKey", "team-700-server"
		property "sonar.projectName", "Team 700 - Server"
		property "sonar.sourceEncoding", "UTF-8"
		// replace "X" hereunder to match your VM url
		property "sonar.host.url", "https://csse-s302g7.canterbury.ac.nz/sonarqube/"
		property "sonar.login", "3dc052fa137c831175f0212b5cf6dec7ea2412d1"
	}
}
